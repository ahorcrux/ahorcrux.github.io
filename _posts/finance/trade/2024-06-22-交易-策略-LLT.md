---
title: 交易策略 | LLT
author: since2014
date: 2023-10-28 18:02:00 +0800
categories: [Finance, Trade, Strategy]
tags: [金融, 交易, 策略]
render_with_liquid: false
---


# 使用技巧

1. 不同周期 Buy 和 Sell 不同, 趋势都是由小周期先出现并扩散到大周期
2. 每个周期设置仓位，15m 、1h、1D 级别，资金杠杆配比 分别为 15% 30倍， 35% 20倍， 50% 10倍，特别的，在1D级别的信号需要关注现货借贷仓位
3. 小周期根据上一级大周期的信号开单，例如：如果 1D 出现 Buy ，那么小周期就是 Buy 开多， Sell 平仓，直到 1D 出现 Sell 信号，就 平掉大周期的单子。小周期趋势反转，那么小周期就是 Sell 开空， Buy 平仓。以此类推。

# TV脚本

## 第一版
```js
//@version=5
strategy("LLT Strategy", overlay=true)

// 可调整的参数a
a = input.float(0.05, title="Alpha")

// 初始化LLT
var float LLT = na

// 计算LLT，如果是第一根K线，则LLT设为该K线的收盘价
price = close
LLT := na(LLT[1]) ? price : (a - math.pow(a, 2) / 4) * price + (math.pow(a, 2) / 2) * nz(price[1]) - (a - (3 / 4) * math.pow(a, 2)) * nz(price[2]) + 2 * (1 - a) * nz(LLT[1]) - math.pow(1 - a, 2) * nz(LLT[2])

// 计算LLT的变化率
deltaLLT = LLT - LLT[1]

// 生成交易信号
longCondition = deltaLLT > 0 and deltaLLT[1] <= 0
shortCondition = deltaLLT < 0 and deltaLLT[1] >= 0

// 执行交易逻辑
if (longCondition)
	if (strategy.position_size < 0)
		strategy.close("Close Short", comment="Close Short")
	strategy.entry("Long", strategy.long, comment="Enter Long")

if (shortCondition)
	if (strategy.position_size > 0)
		strategy.close("Close Long", comment="Close Long")
	strategy.entry("Short", strategy.short, comment="Enter Short")

// 绘制LLT
plot(LLT, color=color.blue, title="LLT")

```

## 常用版

```js
//@version=5
indicator("LLT Indicator", overlay=true)

// 可调整的参数a
a = input.float(0.05, title="Alpha")

// 初始化LLT
var float LLT = na

// 计算LLT
price = close
LLT := na(LLT[1]) ? price : na(LLT[2]) ? price : (a - math.pow(a, 2) / 4) * price + (math.pow(a, 2) / 2) * nz(price[1]) - (a - (3 / 4) * math.pow(a, 2)) * nz(price[2]) + 2 * (1 - a) * nz(LLT[1]) - math.pow(1 - a, 2) * nz(LLT[2])

// 计算LLT的变化率
deltaLLT = LLT - LLT[1]

// 生成交易信号
longCondition = deltaLLT > 0 and deltaLLT[1] <= 0
shortCondition = deltaLLT < 0 and deltaLLT[1] >= 0

// 绘制LLT
plot(LLT, color=color.blue, title="LLT Line")

// 标记买入和卖出信号
plotshape(series=longCondition, location=location.belowbar, color=color.green, style=shape.arrowup, title="Buy", text="Buy")
plotshape(series=shortCondition, location=location.abovebar, color=color.red, style=shape.arrowdown, title="Sell", text="Sell")

// 设置通知
alertcondition(longCondition, title="Buy Alert", message="LLT Buy Signal at {{close}}")
alertcondition(shortCondition, title="Sell Alert", message="LLT Sell Signal at {{close}}")

// 显示交易信号通知
if (longCondition) 
	alert("LLT Buy Signal at " + str.tostring(close), alert.freq_once_per_bar)
if (shortCondition)
	alert("LLT Sell Signal at " + str.tostring(close), alert.freq_once_per_bar)
```

# *参考链接*

